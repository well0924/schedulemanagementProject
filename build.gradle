plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.7'
    id 'io.spring.dependency-management' version '1.1.7'
}
//모든 프로젝트에 적용
allprojects {
    group = 'com.example'
    version = '0.0.1-SNAPSHOT'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    repositories {
        mavenCentral()
    }

}
subprojects {
    apply {
        plugin 'java'
        plugin 'org.springframework.boot'
        plugin 'io.spring.dependency-management'
    }

    tasks.bootJar {
        enabled = false
    }

    jar {
        enabled = true
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }

    String type = project.findProperty('type') ?: ''

    if (type.contains('jpa')) {
        dependencies {
            implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        }
    } else if (type.contains('mvc')) {
        dependencies {
            implementation 'org.springframework.boot:spring-boot-starter-web'

        }
    } else if (type.contains('core')) {
        dependencies {
            implementation 'org.springframework.boot:spring-boot-starter'
        }
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}
